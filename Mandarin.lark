// vim: set syntax=lark: 
// Mandarin compiler
// Copyright (C) 2019  Alexander Korzun
//
// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.
//
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
//
// You should have received a copy of the GNU General Public License
// along with this program. If not, see <https://www.gnu.org/licenses/>.


code: toplevel_statement*

?toplevel_statement: (function_definition | native_function_declaration)? _NL


// Function

native_function_declaration: KW_DEF KW_NATIVE IDENTIFIER "(" typed_arglist ")"

function_definition: KW_DEF IDENTIFIER "(" typed_arglist ")" _NL code_block_end

typed_arglist: (typename? IDENTIFIER ("," typename? IDENTIFIER)* ","?)?

code_block_end: code_statement* KW_END

code_statement: (expression
    | var_declaration
    | var_assignment
    | if_statement
    | for_statement
    | while_statement) _NL


// Variable

var_declaration: typename IDENTIFIER (STRICT_ASSIGNMENT_OP expression)?

var_assignment: expression ASSIGNMENT_OP expression

typename: IDENTIFIER


// If statement

if_statement: KW_IF expression _NL (code_block_elif expression _NL)* (code_block_else _NL)? code_block_end

code_block_elif: code_statement* KW_ELIF

code_block_else: code_statement* KW_ELSE


// For statement

for_statement: KW_FOR IDENTIFIER KW_IN expression _NL code_block_end


// While statement

while_statement: KW_WHILE expression _NL code_block_end


// Expression

expression: front_atomic_expression (BINOP front_atomic_expression)*

?front_atomic_expression: UNOP* atomic_expression

?atomic_expression: literal | IDENTIFIER | "(" expression ")" | atomic_expression call_operator

?literal: NUMBER | STRING_SINGLE | STRING_DOUBLE

call_operator: "(" (expression ("," expression)*)? ")"


KW_DEF:    "def"
KW_ELIF:   "elif"
KW_ELSE:   "else"
KW_END:    "end"
KW_FOR:    "for"
KW_IF:     "if"
KW_IN:     "in"
KW_NATIVE: "native"
KW_WHILE:  "while"

IDENTIFIER.0:    /[a-zA-Z_][a-zA-Z0-9_]*/
NUMBER:        /[0-9]+/
STRING_DOUBLE: /"([^"\\]|\\.)*"/
STRING_SINGLE: /'([^'\\]|\\.)*'/

ASSIGNMENT_OP: STRICT_ASSIGNMENT_OP
    | "+="
    | "-="
    | "*="
    | "/="
    | "//="
    | "%="

STRICT_ASSIGNMENT_OP: "="

BINOP: "*"
    | "/"
    | "%"
    | "//"
    | "+"
    | "-"
    | "..."
    | ".."
    | "=="
    | "<="
    | ">="
    | "!="
    | "<"
    | ">"
    | "&&"
    | "||"
    | "++"

UNOP: "-"
    | "+"
    | "!"
    | "~"

_NL: "\r\n" | "\n"

WS: /[ \t]+/
%ignore WS

COMMENT: /[#][^\n]*/
%ignore COMMENT
